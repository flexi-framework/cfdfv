MODULE MOD_flux_hlle
!===================================================================================================================================
! HLLE flux
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
PRIVATE
!-----------------------------------------------------------------------------------------------------------------------------------
! GLOBAL VARIABLES
!-----------------------------------------------------------------------------------------------------------------------------------
INTERFACE flux_hlle
   MODULE PROCEDURE flux_hlle
END INTERFACE
!-----------------------------------------------------------------------------------------------------------------------------------
PUBLIC  :: flux_hlle
!===================================================================================================================================

CONTAINS

SUBROUTINE flux_hlle( rho_l, rho_r, &
                      v1_l, v1_r,   &
                      v2_l, v2_r,   &
                      p_l, p_r,     &
                      flux_side     )
!===================================================================================================================================
! HLLE flux
!===================================================================================================================================
! MODULES
USE MOD_Globals
USE MOD_Equation_Vars, ONLY: gamma,gamma1,gamma1q
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)             :: rho_l, rho_r
REAL,INTENT(IN)             :: v1_l , v1_r
REAL,INTENT(IN)             :: v2_l , v2_r
REAL,INTENT(IN)             :: p_l  , p_r
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT)            :: flux_side(4)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL                        :: e_l, e_r
REAL                        :: fl(4),fr(4),ul(4),ur(4)
REAL                        :: c_l,c_r
REAL                        :: um
REAL                        :: arp, alm, arp_alm_q
REAL                        :: sqrt_rho_r, sqrt_rho_l, sum_sqrt_rho_q
REAL                        :: rho_lq, rho_rq
REAL                        :: eta2,d
!===================================================================================================================================

! Calculation of auxiliary values
rho_lq         = 1. / rho_l
rho_rq         = 1. / rho_r
sqrt_rho_r     = SQRT(rho_r)
sqrt_rho_l     = SQRT(rho_l)
sum_sqrt_rho_q = 1. / (sqrt_rho_r + sqrt_rho_l)
!-----------------------------------------------------------------------------------------------------------------------------------
! Calculate Energies
e_r = gamma1q * p_r + 0.5 * rho_r * &
      (v1_r * v1_r + v2_r * v2_r)
e_l = gamma1q * p_l + 0.5 * rho_l * &
      (v1_l * v1_l + v2_l * v2_l)
!-----------------------------------------------------------------------------------------------------------------------------------
! Left conservative state vector
ul(RHO) = rho_l
ul(M1) = rho_l * v1_l
ul(M2) = rho_l * v2_l
ul(E) = e_l
! Right conservative state vector
ur(RHO) = rho_r
ur(M1) = rho_r * v1_r
ur(M2) = rho_r * v2_r
ur(E) = e_r
!-----------------------------------------------------------------------------------------------------------------------------------
! Flux in left cell
fl(RHO) = ul(2)
fl(M1) = fl(1)*v1_l+p_l
fl(M2) = fl(1)*v2_l
fl(E) = v1_l*(ul(4)+p_l)
! Flux in right cell
fr(RHO) = ur(2)
fr(M1) = fr(1)*v1_r+p_r
fr(M2) = fr(1)*v2_r
fr(E) = v1_r*(ur(4)+p_r)
!-----------------------------------------------------------------------------------------------------------------------------------
! Calculation of sound speeds
c_l=SQRT(gamma * p_l * rho_lq)
c_r=SQRT(gamma * p_r * rho_rq)
!-----------------------------------------------------------------------------------------------------------------------------------
! Roe mean values
um=(sqrt_rho_r*v1_r+sqrt_rho_l*v1_l) * sum_sqrt_rho_q
!-----------------------------------------------------------------------------------------------------------------------------------
! Signal speeds (Version of Einfeldt paper)
eta2=0.5*sqrt_rho_r*sqrt_rho_l/(sqrt_rho_r+sqrt_rho_l)**2.
d=sqrt((sqrt_rho_r*c_r*c_r+sqrt_rho_l*c_l*c_l)*sum_sqrt_rho_q+eta2*(v1_r-v1_l)*(v1_r-v1_l))
arp=MAX(v1_r+c_r,um+d)
alm=MIN(v1_l-c_l,um-d)
arp_alm_q = 1./(arp-alm)
!-----------------------------------------------------------------------------------------------------------------------------------
! HLLE-Flux
IF (alm > 0.) THEN
  flux_side = fl
ELSEIF (arp < 0.) THEN
  flux_side = fr
ELSE
  flux_side = (arp*fl-alm*fr)*arp_alm_q + (arp*alm)*arp_alm_q*(ur-ul)
END IF
!-----------------------------------------------------------------------------------------------------------------------------------
END SUBROUTINE flux_hlle

END MODULE MOD_flux_hlle
