MODULE MOD_flux_lax_friedrichs
!===================================================================================================================================
! Lax Friedrichs flux
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
PRIVATE
!-----------------------------------------------------------------------------------------------------------------------------------
INTERFACE flux_lax_friedrichs
   MODULE PROCEDURE flux_lax_friedrichs
END INTERFACE
!-----------------------------------------------------------------------------------------------------------------------------------
PUBLIC  :: flux_lax_friedrichs
!===================================================================================================================================

CONTAINS

SUBROUTINE flux_lax_friedrichs( rho_l, rho_r,  &
                                  v1_l, v1_r,    &
                                  v2_l, v2_r,    &
                                  p_l, p_r,      &
                                  flux_side      )
!===================================================================================================================================
! Computation of the Lax-Friechrichs flux
!===================================================================================================================================
! MODULES
USE MOD_Equation_Vars, ONLY: gamma,gamma1q
!--------------------------------------------------------------------------
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)             :: rho_l, rho_r
REAL,INTENT(IN)             :: v1_l , v1_r
REAL,INTENT(IN)             :: v2_l , v2_r
REAL,INTENT(IN)             :: p_l  , p_r
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT)            :: flux_side(4)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL                        :: e_r,e_l 
REAL                        :: a, c_l, c_r
REAL                        :: delta_u(4)
REAL                        :: f1_r(4),f1_l(4)
!===================================================================================================================================

!-----------------------------------------------------------------------------------------------------------------------------------
! Compute maximum eigenvalue 
c_l = sqrt(gamma * p_l / rho_l)
c_r = sqrt(gamma * p_r / rho_r)
a   = max(abs(v1_r) + c_r, abs(v1_l) + c_l)
!-----------------------------------------------------------------------------------------------------------------------------------
! Calculate left/right energy and enthalpy
e_r = gamma1q * p_r + 0.5 * rho_r * (v1_r*v1_r + v2_r*v2_r)
e_l = gamma1q * p_l + 0.5 * rho_l * (v1_l*v1_l + v2_l*v2_l)
!-----------------------------------------------------------------------------------------------------------------------------------
! Calculate the differences of the conservative variables
delta_u(1)  = rho_r      - rho_l
delta_u(2)  = rho_r*v1_r - rho_l*v1_l
delta_u(3)  = rho_r*v2_r - rho_l*v2_l
delta_u(4)  = e_r        - e_l
!-----------------------------------------------------------------------------------------------------------------------------------
! Calculalte the physical fluxes
f1_r(1) = rho_r*v1_r
f1_r(2) = rho_r*v1_r*v1_r + p_r
f1_r(3) = rho_r*v1_r*v2_r
f1_r(4) = v1_r*(e_r + p_r)

f1_l(1) = rho_l*v1_l
f1_l(2) = rho_l*v1_l*v1_l + p_l
f1_l(3) = rho_l*v1_l*v2_l
f1_l(4) = v1_l*(e_l + p_l)
!-----------------------------------------------------------------------------------------------------------------------------------
! Calculate local Lax-Friedrichs flux
flux_side(:) = 0.5 * (f1_r(:) + f1_l(:)) - 0.5 * a * delta_u(:)
!-----------------------------------------------------------------------------------------------------------------------------------
END SUBROUTINE flux_lax_friedrichs

END MODULE MOD_flux_lax_friedrichs
