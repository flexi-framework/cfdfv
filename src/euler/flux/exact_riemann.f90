MODULE MOD_ExactRiemann
!===================================================================================================================================
! Module exact Riemann
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
PRIVATE
!-----------------------------------------------------------------------------------------------------------------------------------
! GLOBAL VARIABLES
!-----------------------------------------------------------------------------------------------------------------------------------
INTERFACE exact_riemann
   MODULE PROCEDURE exact_riemann
END INTERFACE
!-----------------------------------------------------------------------------------------------------------------------------------
PUBLIC  :: exact_riemann
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL        :: G(1:9)
REAL        :: tol   = 1.0E-06
INTEGER     :: nIter = 1000
!===================================================================================================================================

CONTAINS

SUBROUTINE exact_riemann(gamma,rhol,rhor,rho,ul,ur,u,pl,pr,p,al,ar,s)
!===================================================================================================================================
! exact riemann
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)  :: rhol,rhor,ul,ur,pl,pr,al,ar,s    
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT) :: rho, u, p
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
INTEGER          :: kk
REAL             :: du, ducrit, p0, fr, fl, frd, fld, cha, pm, um, gamma
!===================================================================================================================================

G     = (/ (gamma-1.0)/(2.0*gamma), &
           (gamma+1.0)/(2.0*gamma), &
           2.0*gamma/(gamma-1.0),   &
           2.0/(gamma-1.0),         &
           2.0/(gamma+1.0),         &
           (gamma-1.0)/(gamma+1.0), &
           0.5*(gamma-1.0),         &
           1.0/gamma,               &
           gamma-1.0                /)

du = ur-ul
ducrit = G(4)*(al+ar)-du

IF (ducrit .LE. 0.0) THEN
   WRITE(*,*)'Vacuum is generated by given data!!'
   WRITE(*,*)'rhol=',rhol 
   WRITE(*,*)'rhor=',rhor 
   WRITE(*,*)'ul=',ul 
   WRITE(*,*)'ur=',ur 
   WRITE(*,*)'pl=',pl 
   WRITE(*,*)'pr=',pr 
   WRITE(*,*)'al=',al 
   WRITE(*,*)'ar=',ar 
   WRITE(*,*)'du=',du
   WRITE(*,*)'G(4), al+ar',G(4), al+ar
   STOP
END IF

CALL STARTE(p,rhol,rhor,ul,ur,pl,pr,al,ar)

p0  = p
cha = 2.0*tol
KK  = 0

DO WHILE ((cha.GT.tol).AND.(KK.LT.nIter))
   
   KK = KK+1
   
   CALL PREFUN(fl,fld,p,rhol,pl,al)
   CALL PREFUN(fr,frd,p,rhor,pr,ar)
   
   p   = p - (fl+fr+du)/(fld+frd)
   cha = 2.0 * ABS((p-p0)/(p+p0))
   
   IF (p .LT. 0.0) THEN
      p = tol
   END IF
   
   p0=p
   
END DO

IF (KK.GE. nIter) THEN
    WRITE(*,*)'WARNING: Divergence in Newton-Raphson scheme'
END IF

u = 0.5*(ul+ur+fr-fl)

pm = p
um = u

CALL SAMPLE(s,p,u,rho,rhol,rhor,ul,ur,um,pl,pr,pm,al,ar)

!-----------------------------------------------------------------------------------------------------------------------------------
END SUBROUTINE exact_riemann

PURE ELEMENTAL SUBROUTINE STARTE(p,rhol,rhor,ul,ur,pl,pr,al,ar)
!===================================================================================================================================
! 
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)  :: rhol,rhor,ul,ur,pl,pr,al,ar
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT) :: p
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL            ::  pv, pmin,pmax,pnu, pde, qmax,qrat, gel, ger
!===================================================================================================================================
 
qmax = 2.0

pv   = 0.5*(pl+pr)-0.125*(ur-ul)*(rhol+rhor)*(al+ar)
pmin = MIN(pl,pr)
pmax = MAX(pl,pr)
qrat = pmax/pmin

IF ((qrat.LE.qmax).AND.((pmin.LE.pv).AND.(pv.LE.pmax))) THEN
   p = MAX(tol,pv)
ELSE
   IF (pv.LT.pmin) THEN
      pnu = al+ar-G(7)*(ur-ul) 
      pde = al/pl**G(1) + ar/pr**G(1)
      p   = (pnu/pde)**G(3)
   ELSE
      gel = SQRT((G(5)/rhol)/(G(6)*pl+MAX(tol,pv)))
      ger = SQRT((G(5)/rhor)/(G(6)*pr+MAX(tol,pv)))
      p   = (gel*pl+ger*pr-(ur-ul))/(gel+ger)
      p   = MAX(tol,p)
   END IF
END IF
!-----------------------------------------------------------------------------------------------------------------------------------
END SUBROUTINE STARTE

PURE ELEMENTAL SUBROUTINE PREFUN(f,fd,p,rhok,pk,ck)
!===================================================================================================================================
! 
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)  :: p, rhok, pk, ck
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT) :: f, fd
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL             :: prat,ak,qrt,bk    
!===================================================================================================================================

IF (p.LE.pk) THEN
   prat = p/pk
   f    = G(4)*ck*(prat**G(1) - 1.0)
   fd   = (1.0/(rhok*ck))*prat**(-G(2))
ELSE
   ak  = G(5)/rhok
   bk  = G(6)*pk
   qrt = SQRT(ak/(bk+p))
   f   = (p-pk) * qrt
   fd  = (1.0-0.5*(p-pk)/(bk+p))*qrt
END IF
!-----------------------------------------------------------------------------------------------------------------------------------
 
END SUBROUTINE PREFUN

PURE ELEMENTAL SUBROUTINE SAMPLE(s,p,u,rho,rhol,rhor,ul,ur,um,pl,pr,pm,al,ar)
!===================================================================================================================================
! 
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)  :: s,rhol,rhor,ul,ur,um,pl,pr,pm,al,ar
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT) :: p,u,rho
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL             :: pml,pmr,str,stl,cmr,cml,shr,shl,sr,sl,c
!===================================================================================================================================

IF(s.LE.um) THEN
   IF(pm.LE.pl) THEN
      shl=ul-al
      IF(s.LE.shl) THEN
         rho=rhol
         u=ul
         p=pl
      ELSE
         cml = al*(pm/pl)**G(1)
         stl = um-cml
         IF(s.GT.stl) THEN
            rho=rhol*(pm/pl)**G(8)
            u=um
            p=pm
         ELSE
            u=G(5)*(al+G(7)*ul+s)
            c=G(5)*(al+G(7)*(ul-s))
            rho=rhol*(c/al)**G(4)
            p=pl*(c/al)**G(3)
         END IF
      END IF
   ELSE
      pml = pm/pl
      sl  = ul-al*SQRT(G(2)*pml+G(1))
      IF(s.LE.sl) THEN
         rho=rhol
         u=ul
         p=pl
      ELSE
         rho=rhol*(pml+G(6))/(pml*G(6)+1.0)
         u=um
         p=pm
      END IF
   END IF
ELSE
   IF (pm.GT.pr) THEN
      pmr=pm/pr
      sr =ur+ar*SQRT(G(2)*pmr+G(1))
      IF (s.GE.sr) THEN
         rho=rhor
         u=ur
         p=pr
      ELSE
         rho=rhor*(pmr+G(6))/(pmr*G(6)+1.0)
         u=um
         p=pm
      END IF
   ELSE
      shr=ur+ar
      IF (s.GE.SHR) THEN
         rho=rhor
         u=ur
         p=pr
      ELSE
         cmr=ar*(pm/pr)**G(1)
         str=um+cmr
         IF(s.LE.str) THEN
            rho=rhor*(pm/pr)**G(8)
            u=um
            p=pm
         ELSE
            u=G(5)*(-ar+G(7)*ur+s)
            c=G(5)*(ar-G(7)*(ur-s))
            rho=rhor*(c/ar)**G(4)
            p=pr*(c/ar)**G(3)
         END IF
      END IF
   END IF
END IF
!-----------------------------------------------------------------------------------------------------------------------------------
 
END SUBROUTINE SAMPLE

END MODULE MOD_ExactRiemann
