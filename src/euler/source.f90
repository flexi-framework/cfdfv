MODULE MOD_Source
!===================================================================================================================================
! Source terms
!===================================================================================================================================
! MODULES
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! GLOBAL VARIABLES
!-----------------------------------------------------------------------------------------------------------------------------------
INTERFACE Source
   MODULE PROCEDURE Source
END INTERFACE
!-----------------------------------------------------------------------------------------------------------------------------------
! PRIVATE VARIABLES
!-----------------------------------------------------------------------------------------------------------------------------------
INTERFACE EvalSource
   MODULE PROCEDURE EvalSource
END INTERFACE
!-----------------------------------------------------------------------------------------------------------------------------------
PUBLIC   :: Source
PRIVATE  :: EvalSource
!===================================================================================================================================


CONTAINS

SUBROUTINE Source(time)
!===================================================================================================================================
! Source terms
!===================================================================================================================================
! MODULES
USE MOD_Globals
USE MOD_Equation_Vars,ONLY:SourceFunc,CalcSource
USE MOD_Mesh_Vars    ,ONLY:nElems,Elems
USE MOD_Mesh_Vars    ,ONLY:tElem
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL,INTENT(IN)         :: time
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL                    :: src(NVAR)
TYPE(tElem), POINTER    :: aElem
INTEGER                 :: iGP, iElem
!===================================================================================================================================

IF (CalcSource) THEN
  !$omp parallel do private(aElem,iGP,src)
  DO iElem = 1, nElems
    aElem => Elems(iElem)%Elem
    aElem%source = 0.
    DO iGP = 1, aElem%nGP
      CALL EvalSource(SourceFunc, aElem%xGP(iGP,:), time, src)
      aElem%source = aElem%source + src * aElem%wGP(iGP)
    END DO
  END DO 
  !$omp end parallel do
END IF
!-----------------------------------------------------------------------------------------------------------------------------------
END SUBROUTINE Source


SUBROUTINE EvalSource(iSource, x, t, source)
!===================================================================================================================================
! Eval Source
!===================================================================================================================================
! MODULES
USE MOD_Globals
USE MOD_Equation_Vars,ONLY:gamma,pi
!-----------------------------------------------------------------------------------------------------------------------------------
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
INTEGER,INTENT(IN)                 :: iSource
REAL,INTENT(IN)                    :: x(NDIM), t
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL,INTENT(OUT)                   :: Source(NVAR)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES 
REAL                   :: Frequency,Amplitude,Omega,a
!===================================================================================================================================

SELECT CASE(iSource)
CASE(1) ! convergence test 
  Frequency=1.
  Amplitude=0.1
  Omega=Pi*Frequency
  a=2.*Pi
  Source(1)   = (-a+2.*Omega)*COS(omega*SUM(x(:))-a*t)
  Source(2:3) = (-a+Omega*(gamma*3.-1.))*COS(Omega*SUM(x(:))-a*t)        &
              + Amplitude*Omega*(gamma-1.)*SIN(2.*(Omega*SUM(x(:))-a*t))
  Source(4)   = ((2.+gamma*6.)*Omega-4.*a)*COS(omega*SUM(x(:))-a*t)      &
              +Amplitude*(2.*Omega*gamma-a)*SIN(2.*(Omega*SUM(x(:))-a*t))
  Source = Source*Amplitude
CASE DEFAULT
  source = 0.
END SELECT
!-----------------------------------------------------------------------------------------------------------------------------------
END SUBROUTINE EvalSource

END MODULE MOD_Source
